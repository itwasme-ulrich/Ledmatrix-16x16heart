TruyXuatCOTCAO		EQU			1000H
TruyXuatCOTTHAP		EQU			1F00H
TruyXuatHANGCAO		EQU			4000H
TruyXuatHANGTHAP	EQU			9000H
;------------DINH NGHIA O RAM NOI--------------
CHONCOT				EQU			21H		;CHON COT DE QUET
LEDCOUNTER			EQU			25H		;SO LED CON LAI CHUA TAT
TOCDOTAT			EQU			26H		; = 10 <=> 0.1S
POINTER_OFFTABLE	EQU			27H		;VI TRI HIEN TAI TRONG BANG TAT LED
CASEOFF				EQU			28H		
	; 1 CASEOFF <=> 1/2 HANG TAT (HANG CAO HOAC THAP) THUOC 1 COT
	; <=> CO 28 CASE, 28 TRUONG HOP
POINTER_DATARAM		EQU			29H		;CON TRO SU DUNG DE CHUYEN DATA VAO RAM NOI
	
;----------VAI TRO "CHINH" CAC THANH GHI----------
;----R0	   :		POINTER O NHO RAM NOI
;----R1    :		POINTER XUAT GIA TRI HEX DE QUET MA TRAN
;----R2    :		BIEN VONG LAP QUET COT (8 COT 1 LAN QUET)
;----R3    :		BIEN VONG LAP CHUYEN GIA TRI HEX RA 595
;----R4	   :		THOI` GIAN GIU~ NGUYEN TOAN` LED: 100 <=> 1S
;----R5    :		BIEN VONG LAP TOC DO TAT LED :  10 <=> 0.1S
;----R6	   :		BIEN' VONG DELAY THU NHAT
;----R7    :		BIEN' VONG DELAY THU HAI
	
			ORG				0000H
				
			JMP				SET_MODE
			
			ORG				0003H
			JMP				NGATNGOAI0		;TANGTOC
			
			ORG				000BH
			JMP				NGATTIMER0	
			
			ORG				00013H
			JMP				NGATNGOAI1		;RESET SOM
			
	SET_MODE:		
			MOV				TOCDOTAT, #10

			JMP				MAIN
			
			ORG				0030H
	RESET:				
			MOV				SP,#07H
			
MAIN:

			MOV				CASEOFF, #0
			MOV				POINTER_OFFTABLE, #0
			MOV				LEDCOUNTER, #0
		
			
			MOV				TMOD, #01H
			MOV				R4, #100
			MOV				R5, #1
			
			SETB			EA
			SETB			TF0
			SETB			ET0			;CHO NGATTIMER0
			
			SETB			EX1
			SETB			EX0
			
			
			MOV				R0, #30H			
			MOV				POINTER_DATARAM, #0
			MOV				R1, #50H
		HEARTDATA:		
			MOV				A, POINTER_DATARAM
			MOV				DPTR, #HEXTABLE
			MOVC			A, @A+DPTR
			MOV				@R0, A
			
			MOV				A, POINTER_DATARAM
			MOV				DPTR, #LEDCOUNT_TABLE
			MOVC			A, @A+DPTR
			MOV				@R1, A
			
			INC				R1
			INC				R0
			INC				POINTER_DATARAM
			CJNE			R0, #50H, HEARTDATA
			
			MOV				R0, #4DH	;4DH LA O NHO CHUA LED DAU` TIEN CAN TAT
		
			
			
BACK:
			
			MOV				R1,#30H	
			
			MOV				DPTR, #TruyXuatCOTTHAP
			CALL			QUETMATRAN
			MOV				DPTR, #TruyXuatCOTCAO
			CALL			QUETMATRAN
			
			JMP				BACK


DELAY:
			MOV				R7, #2
	LAPDL: 	MOV				R6, 250
			DJNZ			R6, $
			DJNZ   			R7, LAPDL
			RET

QUETMATRAN:
			MOV				CHONCOT,#000000001B
			MOV				R2,#8
	LAPQUET:
			PUSH			83H			
			PUSH			82H
			
			MOV				A, CHONCOT						
			MOVX			@DPTR, A					
			
			MOV				A,@R1
			MOV				DPTR,#TruyXuatHANGCAO			; 3E -> (4000) 
			MOVX			@DPTR, A
			INC				R1
			
			MOV				A,@R1
			MOV				DPTR,#TruyXuatHANGTHAP
			MOVX			@DPTR, A
			INC				R1
			
			CALL			DELAY
			
			POP				82H
			POP				83H
			MOV				A,CHONCOT
			RL				A
			MOV				CHONCOT,A
			
			DJNZ			R2,	LAPQUET
			MOV				A, #00000000B
			MOVX			@DPTR, A
			
			RET

NGATTIMER0:
			CLR				TR0
			MOV				TH0, #HIGH(-9216)
			MOV				TL0, #LOW(-9216)
			SETB			TR0
			
			DJNZ			R4, EXIT
			MOV				R4, #1
			DJNZ			R5, EXIT
;NGATIMER LAN 1: BAT DAU` TAT DAN
	
			MOV				R5, TOCDOTAT
			
			MOV				A, CASEOFF
			CJNE			A, #28, NEXT
			;NEU (28H) = 28 THI DA TAT XONG
			MOV				R0, SP
			MOV				@R0, #HIGH(RESET)	;SP SAU CAT BYTE CAO
			DEC				R0
			MOV				@R0, #LOW(RESET)	;SP TRUOC CAT BYTE THAP
			RETI
	NEXT:
			CALL			HEX_LEDOFF_CONVERT
			
	EXIT:	RETI
			
HEX_LEDOFF_CONVERT:
			PUSH			01H
	
			MOV				A, POINTER_OFFTABLE
			MOV				DPTR, #HEXOFFTABLE
			MOVC			A, @A+DPTR
			MOV				@R0, A
			INC				POINTER_OFFTABLE
		
			MOV				A, #50H
			ADD				A, CASEOFF
			MOV				R1, A
			MOV				A, @R1
			MOV				LEDCOUNTER, A
			
			DJNZ			LEDCOUNTER, EXIT_RETI
			
			DEC				R0
			INC				CASEOFF
		
	EXIT_RETI:	
			MOV				@R1, LEDCOUNTER	
			POP				01H
			RET

NGATNGOAI0:
			MOV				TOCDOTAT,#50
			RETI
	
			
NGATNGOAI1:
			MOV				TOCDOTAT,#10
			MOV				R0, SP
			MOV				@R0, #HIGH(RESET)	;SP SAU CAT BYTE CAO
			DEC				R0
			MOV				@R0, #LOW(RESET)	;SP TRUOC CAT BYTE THAP
			RETI


HEXOFFTABLE:
DB	03CH,038H,030H,020H,000H,078H,070H,060H,040H,000H
DB	07EH,07CH,078H,070H,060H,040H,000H,0FCH,0F8H,0F0H,0E0H,0C0H, 080H,000H
DB	0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H,0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H
DB	0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H,0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H
DB	0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H,0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H
DB	0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H,0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H
DB	0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H,0FEH,0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H
DB	0FCH,0F8H,0F0H,0E0H,0C0H,080H,000H,07EH,07CH,078H,070H,060H,040H,000H
DB	0F8H,0F0H,0E0H,0C0H,080H,000H,03EH,03CH,038H,030H,020H,000H
DB	0F0H,0E0H,0C0H,080H,000H,01EH,01CH,018H,010H,000H
DB	0E0H,0C0H,080H,000H,00EH,00CH,008H,000H
DB	0C0H,080H,000H,006H,004H,000H
DB	080H,000H,002H,000H
DB	000H,000H

LEDCOUNT_TABLE: 
DB	5,5 , 7,7 , 8,8 , 8,8 , 8,8 , 8,8 , 8,8 , 7,7  , 6,6  ,5,5 , 4,4 , 3,3 , 2,2  ,1,1  

HEXTABLE:
	    		;32H
DB	000H,000H,  001H,080H,  003H,0C0H,  007H,0E0H,  00FH,0F0H,  01FH,0F8H,  03FH,0FCH,  07FH,0FEH  
DB	0FFH,0FFH,  0FFH,0FFH,  0FFH,0FFH,  0FFH,0FFH,  0FFH,0FFH,  0FEH,07FH,  07CH,03EH,  000H,000H    
																				;4DH
																				
END			